if command -v brew 2>&1 > /dev/null; then
	if [ -f "$(brew --prefix)/share/chruby" ]; then
		source $(brew --prefix)/share/chruby/chruby.sh
		source $(brew --prefix)/share/chruby/auto.sh
	fi
	if [ -f "$(brew --prefix)/share/gem_home" ]; then
		source $(brew --prefix)/share/gem_home/gem_home.sh
		gem_home $HOME
	fi
fi
# load the aliases in config_files files (optional)
[[ -f "$HOME/Development/config_files" ]] && source ~/Development/config_files/square/aliases

export PATH="$HOME/bin:$(brew --prefix)/opt/coreutils/libexec/gnubin:$PATH:/Applications/Postgres.app/Contents/Versions/latest/bin:/sbin"
export MANPATH="$(brew --prefix)/opt/homebrew/opt/coreutils/libexec/gnuman:$MANPATH"

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

setopt complete_aliases
unsetopt nomatch

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

source $ZSH/git/prompt.sh

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
	source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
	source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
fpath=($ZSH/zsh/completions $fpath)
autoload -U compinit
compinit
# heroku autocomplete setup
CLI_ENGINE_AC_ZSH_SETUP_PATH="$HOME/Library/Caches/heroku/completions/zsh_setup" && test -f $CLI_ENGINE_AC_ZSH_SETUP_PATH && source $CLI_ENGINE_AC_ZSH_SETUP_PATH;

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
	source $file
done

unset config_files

autoload edit-command-line
zle -N edit-command-line
bindkey "^X" edit-command-line

export WORDCHARS="${WORDCHARS:s#/#}"
cdpath=(. $GOPATH/src/github.com/{metmuseum,Hearst-DD,skipjacksc,calebthompson,thoughtbot} ~/code ~/books)

# handy keybindings
bindkey "^A" beginning-of-line
bindkey "^F" forward-word
bindkey "^B" backward-word
bindkey "^E" end-of-line
bindkey "^R" history-incremental-search-backward
bindkey "^P" history-search-backward
bindkey "^Y" accept-and-hold
bindkey "^N" insert-last-word
bindkey -s "^T" "^[Isudo ^[S" # "t" for "toughguy"
bindkey "^[[3~" delete-char # delete forward
bindkey '[[Z' reverse-menu-complete

command -v direnv 2>&1 > /dev/null && eval "$(direnv hook zsh)"
[ -f "$HOME/.ghcup/env" ] && source "$HOME/.ghcup/env"
