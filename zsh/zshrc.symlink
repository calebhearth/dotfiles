export PATH="$HOME/bin:$PATH"

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

setopt complete_aliases

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

source $ZSH/git/prompt.sh

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# handy keybindings
bindkey "^A" beginning-of-line
bindkey "^E" end-of-line
bindkey "^R" history-incremental-search-backward
bindkey "^P" history-search-backward
bindkey "^Y" accept-and-hold
bindkey "^N" insert-last-word
bindkey -s "^T" "^[Isudo ^[A" # "t" for "toughguy"

export PATH="$PATH:$HOME/.rvm/bin" # Add RVM to PATH for scripting
